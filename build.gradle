buildscript {
    ext {
        kotlin_version = '1.3.72'
        kotlin_poet_version = '1.6.0'
        incap_version = '0.3'
        spek_version = '2.0.12'
        junit_version = '5.6.2'
        strikt_version = '0.26.1'
        compile_testing_version = '1.2.9'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.3.72' apply false
    id 'de.jansauer.printcoverage' version '2.0.0' apply false
    id "com.github.ben-manes.versions" version '0.28.0'
}

subprojects { project ->
    project.repositories {
        mavenCentral()
        jcenter()
    }
    project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

dependencyUpdates {

    def reportDir = new File(project.buildDir, 'reports')

    outputFormatter = 'plain'
    outputDir = reportDir.path
    reportfileName = 'dependency-versions'

    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

wrapper { //change gradleVersion and run `gw wrapper` to update
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.5.1'
}

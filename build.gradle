buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.3.72' apply false
    id "com.github.ben-manes.versions" version '0.28.0'
}

subprojects { Project project ->
    project.apply(plugin: 'org.jetbrains.kotlin.jvm')
    project.apply(plugin: 'org.jetbrains.kotlin.kapt')

    project.repositories {
        mavenCentral()
        jcenter()
    }
    project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    project.dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version

        testImplementation("org.spekframework.spek2:spek-dsl-jvm:$spek_version") {
            exclude group: 'org.jetbrains.kotlin'
        }

        testRuntimeOnly("org.spekframework.spek2:spek-runner-junit5:$spek_version") {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }

        testRuntimeOnly group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        testImplementation group: 'io.strikt', name: 'strikt-core', version: strikt_version
    }

    project.test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }

        testLogging {
            exceptionFormat "full"
            events "passed", "failed"

        }
    }
}

tasks.register('publishPluginLocally') {
    dependsOn gradle.includedBuild('plugin').task(':publishToMavenLocal')
}

dependencyUpdates {

    def reportDir = new File(project.buildDir, 'reports')

    outputFormatter = 'plain'
    outputDir = reportDir.path
    reportfileName = 'dependency-versions'

    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

wrapper { //change gradleVersion and run `gw wrapper` to update
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.5.1'
}
